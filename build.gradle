plugins {
    id 'java'
    id 'idea'
    id 'antlr'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'raylras'
version '1.0'
ext.moduleName = 'raylras.zenls'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.antlr:antlr4-runtime:4.10.1'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.14.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'commons-cli:commons-cli:1.5.0'

    antlr 'org.antlr:antlr4:4.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-no-listener", "-long-messages", "-package", "raylras.zen.antlr"]
    outputDirectory = new File(buildDir.toString() + "/generated-src/antlr/main/raylras/zen/antlr")
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'raylras.zen.Main'
    }
    dependencies {
        include(dependency('org.antlr:antlr4-runtime'))
        include(dependency('org.eclipse.lsp4j:org.eclipse.lsp4j'))
        include(dependency('org.eclipse.lsp4j:org.eclipse.lsp4j.generator'))
        include(dependency('org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc'))
        include(dependency('com.google.code.gson:gson'))
        include(dependency('commons-cli:commons-cli'))
    }
    exclude '**/*.java', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF', '**/*.html'
    minimize()
}

jar.enabled = false
build.dependsOn(shadowJar)

task copyServer(type: Copy) {
    from("${getBuildDir()}/libs") {
        include "${rootProject.name}*.jar"
    }
    into './vscode-extension/server'
    rename(/${rootProject.name}.*\.jar/, "${rootProject.name}.jar")
}
